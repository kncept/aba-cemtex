plugins {
    id 'com.kncept.junit.reporter' version '2.0.1'
	id 'java-library'
	id 'maven-publish'
	id 'signing'
}

group = 'com.kncept.abacemtex'
// 'snapshot' versions may not be released to OSSRH
version = '0.0.1'
sourceCompatibility = 14
targetCompatibility = 14
description = """
A Java open source implemention of the ABA Cemtex File format.
Includes builders, parsers and validation.
"""

def junit5version = '5.7.0'

junitHtmlReport {
	failOnEmpty = false
}

repositories {
	mavenLocal() // ./m2/repositories
//	maven { url 'https://repo.spring.io/libs-release'}
	mavenCentral()
	jcenter()
}

java {
	withJavadocJar()
	withSourcesJar()
}

// use JUnit5 Platform for testing
test {
    useJUnitPlatform()
}

// configure the gradle distribiution we use to build the project
// https://docs.gradle.org/current/userguide/gradle_wrapper.html#customizing_wrapper
wrapper {
	distributionType = Wrapper.DistributionType.BIN
	gradleVersion = "6.7.1"
}


dependencies {
	testImplementation "org.junit.jupiter:junit-jupiter-api:" + junit5version
	testImplementation "org.junit.jupiter:junit-jupiter-params:" + junit5version
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:" + junit5version
}

String publishUsername = project.hasProperty('ossrhUsername') ? ossrhUsername : System.getenv("MAVEN_USERNAME")
String publishPassword = project.hasProperty('ossrhPassword') ? ossrhPassword : System.getenv("MAVEN_PASSWORD")

publishing {
	publications {
		mavenJava(MavenPublication) {
			pom {
				name = 'Kncept Cemtex Aba'
				description = 'File Format utility for the ABA Cemtex format'
				url = 'https://github.com/kncept/aba-cemtex'
				properties = [
						myProp: "value",
						"prop.with.dots": "anotherValue"
				]
				licenses {
					license {
						name = 'MIT License'
						url = 'https://github.com/kncept/aba-cemtex/blob/main/LICENSE'
					}
				}
				developers {
					developer {
						id = 'nkrul'
						name = 'Nicholas Krul'
						email = 'nicholas.krul@gmail.com'
					}
				}
				scm {
					connection = 'scm:git:git://github.com/kncept/aba-cemtex.git'
					developerConnection = 'scm:git:ssh://github.com/kncept/aba-cemtex.git'
					url = 'https://github.com/kncept/aba-cemtex'
				}
			}
		}
	}
	repositories {
//		https://docs.github.com/en/free-pro-team@latest/actions/guides/publishing-java-packages-with-gradle
		maven {
			name = "OSSRH"
			url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
			credentials {
				username = publishUsername
				password = publishPassword
			}
		}
	}
}

signing {
	sign publishing.publications.mavenJava
}
tasks.withType(Sign) {
	onlyIf {
//		isReleaseVersion
		new File('gradle.properties').exists() // only sign locally at the moment
	}
}

javadoc {
	if(JavaVersion.current().isJava9Compatible()) {
		options.addBooleanOption('html5', true)
	}
}

